// Fixed-point combinator (Y-combinator) for recursion
const Y = λ recursive. (
    λ x. (x x)
    (λ self_apply. (recursive (self_apply self_apply))) // faster: node `(recursive (self_apply self_apply))` has 2 referenses now
)


// Lists Church encoding
const empty_list = false

const list_cons = λ head_value. λ tail. λ selector. λ default. (
    selector head_value (tail selector default)
)
const list_fold = λ reducer. λ initial_acc. λ lst. (
    lst
        (λ head. λ tail_acc. (reducer head tail_acc))
        initial_acc
)

const list_head = λ lst. (lst true empty_list)

const list_tail = λ lst. (
    list_fold
        false
        empty_list
        lst
)



const list_map = λ transform. λ lst. (
    list_fold
        (λ head. λ tail. (list_cons (transform head) tail))
        empty_list
        lst
)

const list_filter = λ predicate. λ lst. (
    list_fold
        (λ head. λ tail. (
            (predicate head)
                (list_cons head tail)
                tail
        ))
        empty_list
        lst
)

const _make_list = (Y
    λ recursive. λ is_terminator. λ accumulated_list. λ next_value. (
        (is_terminator next_value)
            accumulated_list
            (recursive is_terminator (list_cons next_value accumulated_list))
    )
)
const make_list = λ is_terminator. λ first_value. (
    _make_list is_terminator empty_list first_value
)
const list_concat = λ list1. λ list2. (
    list_fold list_cons list2 list1
)


// Helpers
// Carryed `+` (primitives doesnt works like usual abstractions)
const list_sum = λ lst. (
    list_fold (λ x. λ y. (+ x y)) 0 lst
)

const not = λ val. (val false true)
const sample_list = (make_list (λ x. (= 0 x)) 1 2 3 4 5 6 7 8 9 10 0)
const main = (
    list_sum (list_filter (λ x. (not (= x 100))) (list_map (λ x. (* x x)) (list_concat sample_list sample_list)))
    // 570
)